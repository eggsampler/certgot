{
package nginx

func toString(v interface{}) string {
	s, _ := v.(string)
	return s
}

func toStringSlice(v interface{}) []string {
	var ss []string
	for _, vv := range toIfaceSlice(v) {
	    s := toString(vv)
	    if s == "" {
	        continue
	    }
		ss = append(ss, s)
	}
	return ss
}

func toIfaceSlice(v interface{}) []interface{} {
    if v == nil {
        return nil
    }
    vv, _ := v.([]interface{})
    return vv
}
}

MainDirective = val:Directives EOF
{
    return val, nil
}

Directives = val:( CommentDirective / BlockDirective / SimpleDirective )*
{
    return val, nil
}

CommentDirective "Comment" = whitespace* '#' comment:CommentText
{
    return CommentDirective(strings.TrimSpace(comment.(string))), nil
}

BlockDirective "Block Directive" = whitespace* name:DirectiveName param:(DirectiveParameterList)? whitespace*  '{' whitespace* dirs:Directives whitespace* '}' whitespace*
{
    return BlockDirective{
        Name: toString(name),
        Parameters: toStringSlice(param),
        Children: toIfaceSlice(dirs),
    }, nil
}

SimpleDirective "Simple Directive" = whitespace* name:DirectiveName param:(DirectiveParameterList)? whitespace* ';' whitespace*
{
    return SimpleDirective{
       Name: toString(name),
       Parameters: toStringSlice(param),
    }, nil
}

CommentText = ( !newline . )*
{
    return string(c.text), nil
}

DirectiveName = [a-z_]+
{
    return string(c.text),nil
}

DirectiveParameterList = val:( SingleQuotedParameter / DoubleQuotedParameter / ParameterText )+
{
    return val, nil
}

SingleQuotedParameter = whitespace* val:SingleQuotedString whitespace*
{
    return val, nil
}

DoubleQuotedParameter = whitespace* val:DoubleQuotedString whitespace*
{
    return val, nil
}

ParameterText = whitespace* val:Characters whitespace*
{
    return val, nil
}

SingleQuotedString = `'` ( EscapedChar / (!`'` .) )* `'`
{
    return string(c.text), nil
}

DoubleQuotedString = `"` ( EscapedChar / (!`"` .) )* `"`
{
    return string(c.text), nil
}

Characters = ( EscapedChar / !( space / [;{] ) . )+
{
    return string(c.text), nil
}

EscapedChar = `\` .

space "space" = [ \t]
newline "newline" = [\r\n]
whitespace "whitespace" = space / newline
EOF = !.