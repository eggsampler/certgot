{
package nginx
}

MainDirective = val:Directives EOF
{
    return val, nil
}

Directives = val:( CommentDirective / BlockDirective / SimpleDirective )*
{
    return val, nil
}

CommentDirective "Comment" = _ '#' comment:CommentText
{
    return CommentDirective(strings.TrimSpace(comment.(string))), nil
}

BlockDirective "Block Directive" = _ namez:Name _ paramz:(Parameter)? _ '{' _ dirz:Directives _ '}' _
{
    param, _ := paramz.(string)
    dirs, _ := dirz.([]interface{})
    return BlockDirective{
        Name: namez.(string),
        Parameter: param,
        Children: dirs,
    }, nil
}

SimpleDirective "Simple Directive" = _ namez:Name _ paramz:(Parameter)? _ ';' _
{
    param, _ := paramz.(string)
    return SimpleDirective{
        Name: namez.(string),
        Parameter: param,
    }, nil
}

CommentText = ( ![\r\n] . )* {
    return string(c.text), nil
}

_ "whitespace" = [ \t\r\n]*

Name "Directive Name" = [a-z_]+
{
    return string(c.text), nil
}

Parameter = ( "\\{" / "\\;" / !(_ [;{]) . )+
{
    return string(c.text), nil
}

EOF = !.